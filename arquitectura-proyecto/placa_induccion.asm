LED_R		EQU	P2.0
BUZZER		EQU	P2.1
MAS_BTN		EQU	P3.0
MENOS_BTN	EQU	P3.1
ONOFF_BTN	EQU	P3.2
SENSOR		EQU	P3.3
DISPLAY		EQU	0X29
FUEGO		EQU	0X30
DP_A		EQU	P0.0
DP_B		EQU	P0.1
DP_C		EQU	P0.2
DP_D		EQU	P0.3
DP_E		EQU	P0.4
DP_F		EQU	P0.5
DP_G		EQU	P0.6
ESTADO		EQU	0X20
EVENTO		EQU	0X21
CONT1		EQU	0X27
CONT2		EQU	0X28
TICK100MS	EQU	0X22.0
MS100		EQU	0X23
MS1002		EQU	0X24
TIEMPO		EQU	0X25
IEN0		EQU	0XA8
DP_ENCENDIDO	EQU	0X22.1
APAGANDO	EQU	0X22.2
VECESREPETIR	EQU	0X26
PWMP		EQU	0XFE
PWM0		EQU	0XFC
TICKADC		EQU	0X22.3
TICKTEMP	EQU	0X22.4
ADCON		EQU	0XC5
ADCH		EQU	0XC6
TEMPERATURA	EQU	0X33
CALENTANDO	EQU	0X22.5

ORG 0X00
	AJMP 	INICIO

ORG 0X03
	ACALL	APAGAR_IDL
	RETI

ORG 0x0B
	ACALL 	INT_TIMER
	RETI

ORG 0x53
	ACALL 	INT_ADC
	RETI

ORG 0X80
INICIO:
	ACALL 	INICIALIZACIONES

BUCLE:
	ACALL 	MAQUINAESTADOS
	AJMP	BUCLE

INICIALIZACIONES:
	SETB	IE.0		;habilitamos interrupción externa
	SETB	IE.1		;habilitamos interrupción del timer 0
	SETB	IE.7		;habilitamos interrupcioón total
	MOV	ESTADO,#0
	MOV	EVENTO,#0
	MOV 	CONT1,#0
	MOV 	CONT2,#0
	MOV 	MS100, #0
	MOV	MS1002, #0 
	MOV	TIEMPO, #0 
	MOV	VECESREPETIR, #1
	MOV	FUEGO,#00 
	MOV	DISPLAY,#00 
	MOV	PWMP, #00
	MOV	PWM0, #00
	MOV	TEMPERATURA, #00
	CLR	TICKADC
	CLR	TICKTEMP
	CLR	A
	CLR	C
	CLR	DP_ENCENDIDO
	CLR	APAGANDO
	CLR	LED_R
	CLR	BUZZER
	CLR 	TICK100MS
	CLR	CALENTANDO
	CLR	DP_A
	CLR	DP_B
	CLR	DP_C
	CLR	DP_D 
	CLR	DP_E
	CLR	DP_F
	CLR	DP_G
	CLR	LED_R
	ORL	TMOD,#00000010b
	ANL	TMOD,#11110010b		;INICIALIZAMOS EL TIMER DE 8 BITS
	RET


MAQUINAESTADOS:
	MOV	A,ESTADO
	RL	A
	MOV 	DPTR,#EST_TABL
	JMP 	@A+DPTR

EST_TABL:
	AJMP	ESTADO0			;ESTADO REPOSO
	AJMP	ESTADO1			;ESTADO PITIDO
	AJMP	ESTADO2			;ESTADO PARPADEO
	AJMP	ESTADO3			;ESTADO RECIPIENTE CORRECTO PUESTO
	AJMP	ESTADO4			;ESTADO FOGÓN CALENTANDO Y FUNCIONANDO
	AJMP	ESTADO5			;ESTADO REPOSO TRAS FUNCIONAR (MEDIR TEMP)


;*********************************************** ESTADO 0 --> REPOSO
ESTADO0:
	ACALL	GEN_EVE0		;llama al generador de eventos
	MOV	A,EVENTO
	RL 	A			;ROTA ACUM A LA IZQ
	MOV 	DPTR,#EV0_TBL
	JMP 	@A+DPTR

EV0_TBL:
	AJMP 	EV00_ESPERA		;EVENTO VACIO
	AJMP 	EV01_PULSADOON 		;EVENTO AL PULSAR EL BOTON ON

	

GEN_EVE0:
	SETB 	IEN0.0			;INICIALIZAR MODO IDL
	ORL 	PCON, #0x01
	CLR 	IEN0.0			;SE HA TERMINADO IDL PORQUE SE HA PULSADO ON
	MOV 	EVENTO,#01		;POR LO TANTO NOS VAMOS AL SIGUIENTE ESTADO
	RET

EV00_ESPERA:
	RET

EV01_PULSADOON:
	ACALL	ENCENDER_TIMER		;ENCENDER TIMER
	MOV	FUEGO, #00
	CLR	APAGANDO
	SETB	DP_ENCENDIDO		;ENCENDER DISPLAY
	ACALL	ACTUALIZAR_DP
	ACALL	ACTUALIZAR_PWM
	SETB	LED_R			;ENCENDER LED
	MOV 	ESTADO,#01		;ESTADO PITIDO
	RET

;*********************************************** ESTADO 1 --> PITIDO
ESTADO1:
	ACALL	GEN_EVE1
	MOV	A,EVENTO
	RL 	A			;ROTA ACUM A LA IZQ
	MOV 	DPTR,#EV1_TBL
	JMP 	@A+DPTR

EV1_TBL:
	AJMP 	EV10_ESPERA 		;EVENTO VACIO
	AJMP 	EV11_TERMINARSIGUIENTE	;EVENTO AL PULSAR EL BOTON ON
	AJMP	EV12_SEGUIR		;EVENTO AL NO HABER LLEGADO AL TIEMPO QUE CORRESPONDE, HACE BUCLE
	AJMP	EV13_TERMINARREPOSO	;HA PASADO EL TIEMPO DE ESPERA Y SE APAGA EL DISPOSITIVO

GEN_EVE1:
					;PITIDO
	SETB 	BUZZER
	MOV 	A, #0X2			;introducimos el número 2 en el acumulador
	CLR	C			;limpiamos carry
	SUBB	A, MS100		;acumulador - tick de 100 ms. Si es cero, se habrá llegado a los 200 ms
					;SE HA LLEGADO A LOS 200MS
	JZ	CON11			;si A=0, salta a condición 11
	JB 	TICK100MS,CON12
	MOV 	EVENTO,#0
	RET

EV10_ESPERA:
	RET

CON11:					
	JB	APAGANDO, EV13_TERMINARREPOSO		;SI EL PITIDO ES PORQUE SE ESTA APAGANDO EL DISPOSITIVO VAMOS A REPOSO
	JNB	APAGANDO, EV11_TERMINARSIGUIENTE	;SI EL PITIDO ES PORQUE SE ESTA ENCENDIENDO EL DISPISITIVO SEGUIMOS AL SIGUIENTE ESTADO
	RET

CON12:
	MOV 	EVENTO, #02		;EVENTO SEGUIR
	RET

EV11_TERMINARSIGUIENTE:
	CLR 	BUZZER			;LIMPIAR VALORES TIMER PARA PASAR AL SIGUIENTE ESTADO
	MOV 	MS100, #0		
	CLR 	TICK100MS		;el tick de 100 ms = 0
	MOV 	ESTADO,#02		;ESTADO PARPADEO
	MOV	EVENTO,#00
        RET

EV12_SEGUIR:
 	CLR 	TICK100MS		;BUCLE DE CONTADOR 100MS
	INC 	MS100
	RET

EV13_TERMINARREPOSO:
	ACALL	ENCENDER_ADC		;ENCENDER ADC PARA MEDIR TEMPERATURA
	ACALL	APAGAR_DP		;COMO SE HA APAGADO EL DISPOSITIVO APAGAMOS DP HASTA QUE SE PONGA LO SIGUIENTE CORRESPONDIENTE
	CLR	BUZZER			;LIMPIAMOS VALORES DEL TIMER
	MOV 	MS100,#0
	CLR	TICK100MS
	MOV	ESTADO, #05		;ESTADO MEDIR TEMPERATURA
	MOV	EVENTO, #00		;ESTADO REPOSO INICIAL
	RET

;*********************************************** ESTADO 2 --> PARPADEO
ESTADO2:
        ACALL	GEN_EVE2
        MOV 	A,EVENTO
        RL 	A
        MOV 	DPTR,#EV2_TABL
        JMP 	@A+DPTR


EV2_TABL:
	AJMP 	EV20_ESPERA		;EVENTO ESPERA
        AJMP 	EV21_ENCENDER		;EVENTO ENCENDER DISPLAY
	AJMP	EV22_BUCLE		;EVENTO BUCLE DEL TIMER
	AJMP	EV23_DETECTADO		;EVENTO SENSOR ACTIVADO VER SI SE EMPIEZA A CALENTAR
	AJMP	EV24_TERMINADO		;EVENTO SE HA TERMINADO EL TIEMPO
	AJMP	EV25_APAGARDP		;EVENTO APAGAR DISPLAY
	AJMP	EV26_DETECTADOSEGUIR	;EVENTO SENSOR ACTIVADO SE SIGUE CALENTANDO COMO ANTES

GEN_EVE2:
	JB 	ONOFF_BTN, COND24	;SI SE APAGA EL BOTON NOS VAMOS A VER TEMP
	JB 	SENSOR, COND20		;SI SE ACTIVA EL SENSOR NOS VAMOS A DETECTADO O DETECTADO SEGUIR CALENTANDO
	MOV 	A, #0X5			;CONTADOR DE 500MS / MEDIO SEGUNDO
	CLR	C
	SUBB	A, MS100		;SE HA LLEGADO A LOS 500MS
	JZ	COND21			;CONTADOR DE 15 SEGUNDOS
	MOV	A, #150
	CLR	C
	SUBB	A, MS1002		;SE HA LLEGADO A LOS 15 SEG	
	JZ	COND23
	JB 	TICK100MS,COND22	;SE HA LLEGADO A LOS 100MS
	MOV 	EVENTO,#0
	RET

COND20:
	MOV	A,FUEGO			
	JZ	EV23_DETECTADO		;SI NO SE HA LLEGADO A CALENTAR ANTES VAMOS AL SIGUIENTE ESTADO DE ESPERAR A PULSAR MAS
	JNZ	EV26_DETECTADOSEGUIR	;SI SE HA LLEGADO A CALENTAR ANTES SEGUIMOS CALENTANDO
	RET

COND21:
	JB	DP_ENCENDIDO, EV25_APAGARDP		;SI EL DISPLAY ESTABA ENCENDIDO SE APAGA
	JNB	DP_ENCENDIDO, EV21_ENCENDER		;SI EL DISPLAY ESTABA APAGADO SE ENCIENDE
	RET

COND22:
	MOV 	EVENTO, #0X02
	RET

COND23:
	MOV	MS100,#0 		;VER SI EL CONTADOR TIENE QUE REPETIR LOS 15 SEGUNDOS DEPENDIENDO DEL CASO
	MOV	MS1002,#0		;CASO INICIAL NO TIENE QUE REPETIR
	DEC	VECESREPETIR		;SI VIENE DE CALENTAR REPETIRA OTRA VEZ EL CONTADOR DE 15 SEGUNDOS PARA QUE SEAN 30 SEGUNDOS DE ESPERA		
	MOV	A, VECESREPETIR
	MOV	EVENTO,#00 
	JZ	EV24_TERMINADO		;SE HA ACABADO
	RET

COND24:
	MOV	EVENTO, #04
	RET

EV20_ESPERA:
	RET

EV21_ENCENDER:
	MOV	MS100,#0
	CLR 	TICK100MS
	ACALL	ACTUALIZAR_DP		;SE ENCIENDE AL DISPLAY AL HABER ESTADO APAGADO
	SETB	DP_ENCENDIDO
	RET

EV22_BUCLE:
	CLR	TICK100MS		;BUCLE AL HABER LLEGADO A LOS 100M Y REINICIARLO
	INC	MS100
	INC	MS1002
	RET

EV23_DETECTADO:
	MOV	VECESREPETIR, #04	;PARA EL SIGUIENTE ESTADO DEBE ESPERAR 1 MINUTO ASI QUE SE REPETIRA EL BUCLE 4 VECES
	MOV	ESTADO,#03		;ESTADO RECIPIENTE CORRECTO PUESTO ESPERAR A QUE SE PULSE MAS
	MOV	EVENTO,#00 
	ACALL	ACTUALIZAR_DP
	MOV 	MS100, #0		;PONER A 0 VALORES DEL TIMER
	MOV 	MS1002, #0
	CLR 	TICK100MS
	ACALL	ACTUALIZAR_PWM		;ENCENDER PWM
        RET

EV24_TERMINADO:
	SETB	APAGANDO		;PARA SABER QUE EL PITIDO ES DE APAGADO
	CLR	LED_R			;APAGAR TODO
	ACALL 	APAGAR_PWM
	ACALL	APAGAR_DP
	MOV 	MS100, #0
	MOV 	MS1002, #0
	MOV	VECESREPETIR,#1 	;VUELVE AL CONTADOR INICIAL DE 15 SEG
	CLR	TICK100MS
	MOV	ESTADO,#01 		;ESTADO PITIDO
        RET

EV25_APAGARDP:
	MOV	MS100, #0		;SE APAGA EL DISPLAY AL HABER ESTADO ENCENDIDO
	CLR 	TICK100MS
	CLR	DP_ENCENDIDO
	ACALL	APAGAR_DP
	RET

EV26_DETECTADOSEGUIR:
	ACALL 	ACTUALIZAR_PWM		;SIGUE CALENTANDO COMO LO HACIA ANTES DE HABERSE QUITADO EL RECIPIENTE
	MOV	VECESREPETIR, #02	;AHORA LA ESPERA SERA DE 30 SEGUNDOS
	MOV	ESTADO,#04		;ESTADO DE FOGON ENCENDIDO Y FUNCIONANDO
	MOV	EVENTO,#00
	ACALL	ACTUALIZAR_DP
	MOV 	MS100, #0		;PONER A 0 VALORES DEL TIMER
	MOV 	MS1002, #0
	CLR 	TICK100MS
	RET


;*********************************************** ESTADO 3 --> RECIPIENTE CORRECTO PUESTO
ESTADO3:
        ACALL	GEN_EVE3
        MOV 	A,EVENTO
        RL 	A
        MOV 	DPTR,#EV3_TABL
        JMP 	@A+DPTR


EV3_TABL:
	AJMP 	EV30_ESPERA		;EVENTO ESPERA
        AJMP 	EV31_1MIN		;EVENTO AL HABER PASADO UN MINUTO DE ESPERA
	AJMP	EV32_BUCLE		;EVENTO DEL BUCLE DEL CONTADOR
	AJMP	EV33_FUEGO		;EVENTO AL HABERSE PULSADO EL BOTON DE MAS

GEN_EVE3:
	JB 	ONOFF_BTN, COND34	;SI SE HA PULSADO EL BOTON DE APAGAR SE APAGA EL DISPOSITIVO PASANDO AL ESTADO DE PTIIDO
	JB	MAS_BTN, COND33		;SI SE HA PULSADO EL BOTON DE MAS VAMOS AL ESTADO DEL FOGON FUNCIONANDO
	MOV	A, #150
	CLR	C
	SUBB	A, MS100		
	JZ	COND31			;MIRA SI SE HAN LLEGADO A LOS 15 SEGUNDOS
	JB 	TICK100MS,COND32	;SI SE HAN LLEGADO A LOS 100MS
	MOV 	EVENTO,#0
	RET


COND31:
	MOV	MS100,#0 		;SI SE HA LLEGADO A LOS 15 SEGUNDOS SE SIGUE REPITIENDO HASTA QUE SEA 1 MINUTO
	DEC	VECESREPETIR		;POR ESO SE DECREMENTA LAS VECES QUE SE REPITE
	MOV	A, VECESREPETIR
	MOV	EVENTO,#00 
	JZ	EV31_1MIN		;SI HA PASADO EL MINUTO
	RET

COND32:
	MOV	EVENTO, #0X02
	RET

COND33:
	MOV 	A, FUEGO		;SI HABIA FUEGO DE ANTES (DISTINTO DE 0) NOS VAMOS A SEGUIR CALENTANDO
	JZ 	EV33_FUEGO
	RET

COND34:
	MOV	EVENTO, #01
	RET

EV30_ESPERA:
	RET

EV31_1MIN:
	SETB	APAGANDO		;HA PASADO UN MINUTO Y POR LO TANTO SE APAGA EL DISPOSITIVO
	CLR	LED_R
	ACALL	APAGAR_DP
	MOV 	MS100, #0		;SE PONEN A 0 LOS VALORES DEL TIMER
	CLR	TICK100MS
	MOV	ESTADO,#01		;ESTADO DEL PITIDO
        RET

EV32_BUCLE:
	CLR	TICK100MS		;SE HACE LE BUCLE DE LOS 100MS
	INC	MS100
	RET

EV33_FUEGO:
	SETB	CALENTANDO		;SE PONE CALENTANDO PARA SABER QUE ESTAMOS EMPEZANDO A TENER EL FOGON FUNCIONANDO
	MOV	VECESREPETIR,#1 	;SE PONE DE NUEVO EL CONTADOR SOLO A 15 SEGUNDOS
	MOV	FUEGO,#01 
	ACALL	ACTUALIZAR_DP
	MOV 	MS100, #0
	CLR 	TICK100MS
	MOV	ESTADO,#04		;ESTADO DEL FOGON FUNCIONANDO
	MOV	EVENTO, #00
        RET
;*********************************************** ESTADO 4 --> FOGON FUNCIONANDO
ESTADO4:
        ACALL	GEN_EVE4
        MOV 	A,EVENTO
        RL 	A
        MOV 	DPTR,#EV4_TABL
        JMP 	@A+DPTR


EV4_TABL:
	AJMP	EV40_ESPERA		;EVENTO VACIO
	AJMP	EV41_RESTAR		;EVENTO AL HABERSE PULSADO EL BOTON DE RESTAR
	AJMP	EV42_SUMAR		;EVENTO AL HABERSE PULSADO E LBOTON DE SUMAR
	AJMP	EV43_REPOSOH		;EVENTO PARA IR A REPOSO CONTANDO LA TEMPERATURA
	AJMP	EV44_SINRECIPIENTE	;EVENTO AL HABER QUITADO EL RECIPIENTE

GEN_EVE4:
	JB	ONOFF_BTN, COND43	;SI SE PULSA EL BOTON DE APAGADO
	JB	SENSOR, COND40		;SI HAY OLLA PUESTA
	JNB	SENSOR, COND44		;SI NO HAY OLLA PUESTA
	RET

EV40_ESPERA:
	RET

COND40:
	JB	MAS_BTN, CON42		;SI SE HA PULSADO SUMAR
	JB	MENOS_BTN, CON41	;SI SE HA PULSADO RESTAR
	RET

COND44:
	MOV 	EVENTO,#04
	RET 
CON41:
	MOV 	A,FUEGO			;SI ESTAMOS EN 0 NO SE PUEDE RESTAR MAS
	JNZ 	EV41_RESTAR
	RET

CON42:
	MOV 	A,FUEGO			;SI ESTAMOS EN P (10) NO SE PUEDE SUMAR MAS
	CJNE	A,#10D,EV42_SUMAR	;SOLO SUMA SI EL DP NO MUESTRA P
	RET

COND43:
	MOV 	EVENTO, #03
	RET

EV42_SUMAR:
	ACALL	SUMAR			;REALIZAR SUMA
	RET

EV41_RESTAR:
	JNZ	RESTAR			;REALIZAR RESTA
	RET

EV43_REPOSOH:
	SETB	APAGANDO		;APAGAMOS TODO PORQUE NOS VAMOS A LREPOSO DE MEDIR TEMPERATURA
	CLR	LED_R
	ACALL 	APAGAR_PWM
	ACALL	APAGAR_DP
	MOV	VECESREPETIR, #01
	MOV	ESTADO,#01 		;PERO PRIMERO SE PASA POR EL ESTADO DE PTIIDO
	RET

EV44_SINRECIPIENTE:
	ACALL 	APAGAR_PWM		;SI NO HAY RECIPIENTE SE APAGA PWM
	MOV	VECESREPETIR, #02	;SE PONE EL CONTADOR A 30 SEGUNDOS
	MOV	ESTADO, #02		;NOS VAMOS AL ESTADO DE PARPADEO DONDE PARPADEA EL NUMERO PUESTO EN EL FOGON 
	RET

SUMAR:
	INC	FUEGO			;SUMAR
	ACALL	ACTUALIZAR_DP
	ACALL	ACTUALIZAR_PWM
	RET

RESTAR:
	DEC	FUEGO			;RESTAR
	ACALL	ACTUALIZAR_DP
	ACALL	ACTUALIZAR_PWM
	RET

;*********************************************** ESTADO 5 --> REPOSO TRAS FUNCIONAR

ESTADO5:
        ACALL	GEN_EVE5
        MOV 	A,EVENTO
        RL 	A
        MOV 	DPTR,#EV5_TABL
        JMP 	@A+DPTR


EV5_TABL:
	AJMP	EV50_ESPERA		;EVENTO VACIO
	AJMP	EV51_COMPROBARTEMP	;EVENTO PARA COMPROBAR LA TEMPERATURA
	AJMP	EV52_MOSTRARHMAYUS	;EVENTO PARA CAMBIAR DISPLAY A H
	AJMP	EV53_MOSTRARHMINUS	;EVENTO PARA CAMBIAR EL DISPLAY A h
	AJMP	EV54_MENOR40		;EVENTO CUANDO ES MENOS DE 40 GRADOS Y HAY QUE PASAR AL REPOSO INICIAL	
	AJMP	EV55_SEGUIR		;EVENTO DEL BUCLE DEL CONTADOR DE 500MS

GEN_EVE5:
	JNB	CALENTANDO, COND52
	MOV 	A, #0X5
	CLR	C
	SUBB	A, MS100
	JZ	COND51			;HEMOS LLEGADO A LOS 500MS
	JB 	TICK100MS,COND55	;SE HA LLEGADO A LOS 100MS HACE BUCLE
	JBC	TICKTEMP, COND52	;SI ESTAMOS BAJO 40 GRADOS DEJAMOS DE MEDIR Y VAMOS A REPOSO
	RET

EV50_ESPERA:
	RET

COND51:
	MOV 	EVENTO, #01
	RET

COND52:
	MOV 	EVENTO, #04
	RET

COND55:
	MOV	EVENTO, #05
	RET

COMPROBAR_TEMP:
	MOV	MS100, #0		;PONER A 0 VALORES DEL TIMER
	CLR 	TICK100MS
	ACALL	VER_TEMP		;MIRAR TEMPERATURA
	MOV	A, TEMPERATURA
	CLR 	C
	SUBB	A,#41D
	JC	MENOR80 		;MENOR A 40 GRADOS
	JNC	MAYOR80			;MAYOR A 40 GRADOS
	MOV	EVENTO, #00
	RET

EV51_COMPROBARTEMP:
	MOV	MS100,#0 
	ACALL 	ENCENDER_ADC
	ACALL 	COMPROBAR_TEMP
	RET

EV52_MOSTRARHMAYUS:
	MOV 	FUEGO, #12		;ACTUALIZAMOS DISPLAY PARA H
	ACALL	ACTUALIZAR_DP
	MOV	EVENTO, #00
	RET

EV53_MOSTRARHMINUS:
	MOV	FUEGO, #11 		;ACTUALIZAMOS DISPLAY PARA h
	ACALL	ACTUALIZAR_DP
	MOV	EVENTO, #00
	RET

EV54_MENOR40:
	CLR	CALENTANDO		;APAGAR TODO NOS VAMOS A REPOSO INICIAL
	CLR	TICKTEMP
	MOV	ESTADO,#00
	ACALL	APAGAR_DP
	RET

EV55_SEGUIR:
 	CLR 	TICK100MS		;BUCLE 100 MS SEGUIR
	INC 	MS100
	MOV	EVENTO, #00
	RET

MAYOR80:
	MOV 	EVENTO,#02 		;SI ES MAYOR A IR AL EVENTO DE H
	RET

MENOR80:
	MOV	A, TEMPERATURA		;SI ES MENOR A 80 VER SI ESTA ENTRE O ES MENOR A 40
	CLR 	C
	SUBB	A,#20D
	JC	MENOR40			;SI ES MENOR A 40
	JNC	MAYOR40YMENOR80		;SI ESTA ENTRE
	RET

MENOR40:
	SETB	TICKTEMP
        RET

MAYOR40YMENOR80:
	MOV EVENTO, #03			;SI ESTA ENTRE IR AL EVENTO DE h
	RET
;------------------------------------------------------------------------------------------------------------------------------------------------



;*********************************************** DISPLAY
ACTUALIZAR_DP:
	MOV	A,FUEGO			;MOSTRAR LO QUE HAY GUARDADO EN FUEGO QUE ES EL NIVEL DEL FOGON
	JZ	MOSTRAR_DP_0		;0 SI TODAVIA NO SE HA CALENTADO NADA
	ACALL	BCD_7SEG
	MOV 	DISPLAY, A
	MOV	P0, DISPLAY
	RET

MOSTRAR_DP_0:
	MOV 	FUEGO, #00		;QUE MUESTRE 0
	MOV	A, #00
	ACALL 	BCD_7SEG
	MOV 	DISPLAY, A
	MOV 	P0, DISPLAY
	RET

APAGAR_DP:
	CLR 	DP_ENCENDIDO		;APAGAR DISPLAY
	MOV	A, #13
	ACALL 	BCD_7SEG
	MOV 	P0, A
	CLR	A
	RET

BCD_7SEG:
	INC 	A
	MOVC 	A,@A+PC
	RET
	DB 	00111111b 		;0
	DB 	00000110B 		;1
	DB 	01011011B 		;2
	DB 	01001111B 		;3
	DB 	01100110b 		;4
	DB 	01101101B 		;5
	DB 	01111101B 		;6
	DB 	00000111B 		;7
	DB 	01111111B 		;8
	DB 	01100111B 		;9
	DB	01110011B 		;P
	DB	01110100B 		;h
	DB 	01110110B 		;H
	DB	00000000B 		;display apagado

;*********************************************** PWM

ACTUALIZAR_PWM:			
	MOV 	PWMP, #04		
	MOV	A, FUEGO		;OBTENER VALOR DE PWM A PARTIR DEL % DE POTENCIA 
	ACALL 	PWM_TABLA	
	MOV 	PWM0,A
	RET

APAGAR_PWM:			
	MOV 	PWMP, #04		;APAGAR PWM
	MOV	A,  #00
	ACALL 	PWM_TABLA	
	MOV 	PWM0,A
	RET

PWM_TABLA:
	INC 	A
	MOVC 	A,@A+PC
	RET
	DB 	11111111B 		;0 FF
	DB 	11100110B 		;1 E6
	DB 	11001100B 		;2 CC
	DB 	10110011B 		;3 B3
	DB 	10011001b 		;4 99
	DB 	10000000B 		;5 80
	DB 	01100110B 		;6 66
	DB 	01001101B 		;7 4D
	DB 	00110011B 		;8 33
	DB 	00011010B 		;9 1A
	DB	00000000B 		;P 00


;*********************************************** ADC

ENCENDER_ADC:
	ANL 	ADCON,#11111000b	;INICIALIZACION DEL ADC
	ORL 	ADCON,#00001000b
	ORL 	IEN0,#11000000b
	RET

APAGAR_ADC:
	ANL 	IEN0, #00000000b	;APAGAR ADC
	RET

VER_TEMP:
	CLR 	TICKADC			;MEDIR LO QUE HAY EN ADC0
	MOV	TEMPERATURA,ADCH
	CLR 	C
	RET

INT_ADC:
	SETB 	TICKADC
	RET

;*********************************************** TIMER 0	

;Ftimer = Fosc / 12 = 2MHz 
;Ttimer = 1 / Ftimer = 1/2000000 s
;objetivo = calcular 100 ms para poder llegar a todos los requerimientos temporales (200ms, 500ms, 15s, 60s)
;0,1s = x ciclos * Ttimer
;x = 200 000 ciclos = 200 * 125 * 8
;200 = 256 - 200 -> 56 de precarga en el timer
;contador 1 = 125
;contador 2 = 8
;El contador 1 contará las veces que el timer llega a 200. Una vez alcance los 125, el contador 2 empezará a contar 8 veces más. Así se alcanzarán 200 000 ciclos y se activará la flag de 100 ms.


ENCENDER_TIMER:			
	ORL 	IEN0,#10000010b		;INICIALIZACIONES DEL TIMER con máscara OR que activa los bits IE.7 e IE.1 del registro de interrupciones
	MOV	TH0, #00111000b		;56 precarga
	MOV 	TL0, #00111000b		;56 precarga
	MOV 	CONT1,#0		;limpiamos contador 1
	MOV 	CONT2,#0		;limpiamos contador 2
	SETB 	TR0			;bit del registro TCON (TCON.4) timer 0 run control bit. Set/cleared by software to turn timer 0 ON/OFF
	RET

APAGAR_TIMER:			
	ANL 	IEN0, #00000000b	;APAGAR TIMER con máscara AND que pone todos los bits del registro de interrupciones a 0. 
        CLR 	TR0			;timer 0 will be turned OFF
	RET


INT_TIMER:	
	PUSH 	PSW
	PUSH 	ACC
	INC 	CONT1			; incrementamos el primer contador
	MOV 	A, #01111101b 		;125 al acumulador
	CLR 	C			
	SUBB 	A, CONT1		;acumulador - cont1 
	JNZ 	TIMERCON		;si el acumulador != de 0, salta
	MOV 	CONT1, 	#0		; si el cont1 ha llegado a 125, lo limpia. Esto significa que han transcurrido 125*200 ciclos
	INC 	CONT2			; incrementa contador 2
	MOV 	A, #00001000b 		;8 al acumulador
	CLR 	C			;limpiamos carry
	SUBB 	A, CONT2		;acumulador - cont2
	JNZ 	TIMERCON		; si es diferente de 0, salta
	MOV 	CONT2, #0		; si el cont2 ha llegado a 8, lo limpia. Esto significa que han transcurrido 125*200*8 ciclos, los necesarios
	SETB 	TICK100MS		;HAN PASADO 100MS, se activa la flag de llegada y salimos de la rutina
	POP 	ACC			
	POP 	PSW
	RET

TIMERCON:
	POP 	ACC
	POP 	PSW
	RET

;*********************************************** IDL
	
APAGAR_IDL:			
	PUSH PSW			;APAGAMOS IDL CUANDO SE HA SALIDO DEL MODO IDL
	PUSH ACC			;NO NOS HACE FALTA UN ENCENDER IDL PORQUE SE HACE DIRECTAMENTE EN EL ESTADO DE REPOSO	
	ANL PCON, #0XFE
	POP ACC
	POP PSW
	RET
	
;*********************************************** FIN
END
